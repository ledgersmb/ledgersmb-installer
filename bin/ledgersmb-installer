#!/usr/bin/env perl


BEGIN {
   local $SIG{__DIE__} = sub { warn @_; exit 1; };
   if ($] < 5.034) {
     die "Perl version too low ($^V); v5.34 required\n";
   }
}

use v5.34; # because of try/catch
BEGIN { unshift @INC, 'lib'; } # almost, but not quite, "use lib 'lib';"


my $failed_validate_msg = <<'EOF';

You may need to install full Perl support for your environment; for Linux,
you need to install one of the following packages, depending on your
distribution:

- perl (Debian, Ubuntu and derivatives)
- perl (Fedora 27+, RedHat 8+ and derivatives)
- perl-core (Fedora before 27, RedHat before 8 and derivatives)


(Submit your extensionst to the above list to
   https://github.com/ledgersmb/ledgersmb-installer/issues)


EOF

sub validate_perl_env {
    my (@missing);
    for my $mod (qw( experimental lib parent
                     File::Path File::Spec File::Temp
                     Getopt::Long HTTP::Tiny )) {
        my $file = "$mod.pm" =~ s/::/\//gr;
        eval { require $file; 1 }
        or do {
            if ($@ =~ /^Can't locate \Q$file\E in \@INC/) {
                push @missing, $mod;
            }
            else {
                warn $@;
            }
        }
    }

    if (@missing) {
        say 'Your installation is missing these modules:';
        say "   $_" for (@missing);
        say $failed_validate_msg;

        die "Aborting setup...\n";
    }
}

validate_perl_env();
if (require LedgerSMB::Installer) {
    exit LedgerSMB::Installer->run(@ARGV);
}

say "Failed to load the installer:\n\n$@";
exit 1;
